<!DOCTYPE html>
<html>
	<head>
		<meta charset="utf-8">
		<title>Service Worker tools</title>
		<meta name="description" content="servie worker tools">
		<meta name="viewport" content="width=device-width, initial-scale=1">
		<meta name="mobile-web-app-capable" content="yes">

		<link rel="stylesheet" href="/shared/css/codemirror.css">
		<link rel="stylesheet" href="/shared/css/vscode.codemirror.css">
		<script src="/shared/vendor/codemirror.js"></script>
		<script src="/shared/vendor/codemirror/mode.bundle.js"></script>
		<script src="ink.mode.js"></script>

		<style>
			body {
				margin: 0; overflow: hidden;
				margin-right: -15px;
			}
			#editor {
				width: 100%;
				height: 100vh;
				border: 1px solid black;
			}
			.CodeMirror {
				height: 100%;
			}
		</style>
	</head>

	<script id="source" type="text/ink">
`
block comment
`
`` line comment

memo := [0, 1]
fibMemo := n => (
	memo.(n) :: {
		() -> memo.(n) := fibMemo(n - 1) + fibMemo(n - 2)
	}
	memo.(n)
)
fibDriver := n => map(
	range(0, n + 1, 1)
	fibMemo
)
each(
	fibDriver(9)
	n => out(n + ' ')
)
log('\n')


std := load('../vendor/std')
log := std.log


`` ----------------------------
log('hello world')
` a single line comment `
`
a multi-line comment
`
`` log('This should not run!')

`` ----------------------------

std := load('../vendor/std')
log := std.log

length := (0.5 + 2 * 3 / 4 - 5) + ~6 % 7
log(length)

` a negative literal `
temperature := ~5
log(temperature)

year := pow(10, 3) * 2 + 20
log(year)

name := 'ada' + ' ' + 'lovelace'
log(name)
name.0 := 'A'
name.len(name) := '!'
log(name)

authed := true
log(authed)
log(~authed)
log(true | false)
log(true & false)

exists := ()
log(exists)

primes := [2, 3, 5, 7]
log(primes)

menu := {
    apples: 1.00
    oranges: 1.50
}
log(menu)
log(type(0))

`` ----------------------------

std := load('../vendor/std')
scan := std.scan
log := std.log
f := std.format

out('Hello, with a manual linebreak.
')
log(7) `` print a number
log([1, 2]) `` a composite
log(() => ()) `` a function
log(()) `` or null

prompt := (msg, cb) => (
    log(msg)
    scan(cb)
)
` usage:
prompt('Are you sure?', msg => (
    log(msg)
))`

log(f('A message without any passed in values.', {}))
log(f('The time is: {{ time }}.', {time: time()}))


`` ----------------------------

std := load('../vendor/std')
log := std.log
reduce := std.reduce
each := std.each
map := std.map

numbers := [0, 1, 2, 3, 4, 5, 6]

logger := num => log(num)
each(numbers, logger)

squares := map(numbers, num => num * num)
log(squares)

sum := reduce(squares, (acc, num) => acc + num, 0)
log(sum)

`` ----------------------------

std := load('../vendor/std')
filter := std.filter
log := std.log

error := true
error :: {
    false -> log('No problem!')
    true -> log('There is a problem..')
}

device := 'windows'
device :: {
    'linux' -> log('Linux!')
    'macOS' -> log('Mac!')
    _ -> log('Neither Linux or Mac.')
}

first := {
    code: 2,
    user: 'alice@google'
}
second := {
    code: 2,
    user: 'claire@amazon'
}

checkEvent := event => event :: {
    {code: 2, user: _} -> log('All good!')
    _ -> log('Bad event..')
}
checkEvent(first)
checkEvent(second)

numbers := [1, 2, 3, 4, 5, 6]
onlyEven := num => num % 2 :: {
    0 -> true
    _ -> false
}
even := filter(numbers, onlyEven)
log(even)

`` ----------------------------

std := load('../vendor/std')
log := std.log
reverse := std.reverse
slice := std.slice
range := std.range
each := std.each
join := std.join


Lists can be declared with array syntax.

names := ['Alice', 'Andrew']
log(names)

log(names.0) `` first element
log(names.(len(names) - 1)) `` last element

names.0 := 'Madeline'
log(names)

names.len(names) := 'Francisca'
log(names)

indexer := (name, index) => (
    log(index)
    log(name)
)
each(names, indexer)

numbers := range(0, 6, 1)
log(numbers)

half := slice(numbers, 0, 3)
log(half)

first := [98, 99]
second := [100, 101]
third := join(first, second)
log(third)

log(reverse(['one', 'two', 'three', ]))

`` ----------------------------

std := load('../vendor/std')
clone := std.clone
each := std.each
log := std.log


observation := {
    weather: 'Sunny'
    'observedAt': {
        time: time()
    }
}
log(observation)

observation.('weather') := 'Raining'

observation.weather := 'Raining'
log(observation)

cloned := clone(observation)

each(keys(observation), (key) => (
    log(observation.(key))
))

`` -----------------------------

std := load('../vendor/std')
log := std.log

shout := (word) => word + '!'
log(shout('Hello'))

multiply := (x, y) => x * y
log(multiply(2, 8))

` single argument parentheses are optional`
sayAndMultiply := num => (
    log('About to multiply ' + string(num) + ':')

    ` the return value `
    num * num
)
log(sayAndMultiply(8))

math := {
    swapSign: n => 0 - n
}
weight := (math.swapSign)(100)
log(weight)

factorial := n => n :: {
    0 -> 1
    _ -> n * factorial(n-1)
}
log(factorial(5))

makeMultiplier := x => (
    y := y => x * y
)
multiplySeven := makeMultiplier(7)
multiplyTwenty := makeMultiplier(20)
log(multiplySeven(2))
log(multiplyTwenty(4))

greeting := a => b => a + ' ' + b
message := greeting('Hello there,')('General Kenobi')
log(message)

`` --------------------------

std := load('../vendor/std')
writeFile := std.writeFile
readFile := std.readFile
slice := std.slice
log := std.log

readFile('../README.md', data => (
    log('Read:')
    log(
    ` data is the entire file content `
    slice(data, 0, 16))
))

readFile('../doesnt-exist.md', data => data :: {
    () -> log('The read failed.')
    _ -> log('The read succeeded.')
})

path := '../tmp/_writeExample.txt'
data := 'Some data. '
writeFile(path, data, cb => cb :: {
    ` either true or null `
    true -> log('The write succeeded.')
    _ -> log('The write failed.')
})

make('../tmp/_makeExample/', cb => cb.type :: {
    'error' -> (
        log('Make:')
        log('  ' + string(cb.message))
    )
    _ -> log('Make succeeded.')
})

dir('../src/', cb => cb.type :: {
    'error' -> log('Dir failed.')
    ` cb.data is a list of files and directories`
    _ -> (
        log('Dir:')
        log('  ' + string(cb.data.0))
    )
})

stat('../README.md', cb => cb.type :: {
    'error' -> log('Stat failed.')
    _ -> (
        log('Stat:')
        log('  ' + string(cb))
    )
})

`` --------------------------

std := load('../vendor/std')
log := std.log

t := time()
data := {
    method: 'GET'
    url: 'https://example.org'
    headers: {}
    body: ''
}

close := req(data, evt => evt.type :: {
    ` this matches during a request fail 
      or if the request is aborted `
    'error' -> log('Error: ' + evt.message)
    _ -> (
        log('Req: ')
        elasped := string(time() - t)
        log('  elapsed time: ' + elasped)
        log('  ' + string(keys(evt.data)))
    )
})
log('This code runs while we request.')
`` close() would abort the request

resp := {
    status: 200
    headers: {'Content-Type': 'text/plain'}
    body: 'Hello from Ink land!'
}
close := listen('0.0.0.0:80', evt => evt.type :: {
    'error' -> log('Error: ' + evt.message)
    'req' -> (evt.end)(resp)
})
log('This code runs while we listen.')

` stop the server `
close()

`` --------------------------

std := load('../vendor/std')
range := std.range
each := std.each
log := std.log

choices := [rand(), rand(), rand()]
log(choices)

` an ordered list `
numbers := range(0, 10, 1)

shuffle := list => (
    each(list, (val, i) => (
        pick := floor(rand() * (len(list) - i - 1))
        current := len(list) - 1 - i
        tmp := list.(current)
        list.(current) := list.(pick)
        list.(pick) := tmp
    ))
    list
)
log(shuffle(numbers))

length := 10
log(urand(10))

`` --------------------------

std := load('../vendor/std')
log := std.log

qs := load('../vendor/quicksort')
sortBy := qs.sortBy
sort! := qs.sort!
sort := qs.sort


numbers := [921, 14, 0, 0.002]
log(sort(numbers))

temperatures := [33, 1, 5, 22]
sort!(temperatures)

` the existing list has changed `
log(temperatures)

menu := {
    apples: 1.50
    oranges: 2.00
    coconut: 3.50
    grapes: 1.75
}
items := ['apples', 'oranges', 'coconut', 'grapes']

` sort items by their menu price `
log(sortBy(items, item => menu.(item)))

`` --------------------------

std := load('../vendor/std')
each := std.each
log := std.log

path := 'ls' `` path to executable
arguments := []
stdin := ''
stdoutFn := out => log(out)
exec(path, arguments, stdin, stdoutFn)

log('Arguments:')
each(args(), arg => log('  ' + arg))

	</script>


	<body>
		<div id="editor"></div>
	</body>

	<script type="module">
		const dummyText = document.querySelector('#source').textContent;

		const setupEditor = function(){
			const editorDiv = document.querySelector('#editor');
			const cm = CodeMirror(editorDiv, {
				lineNumbers: true,
				tabSize: 2,
				mode: 'ink',
				theme: 'vscode-dark',
				value: dummyText,
			});
			cm.setSize(null, Infinity);
		};

		setupEditor();
	</script>

</html>
